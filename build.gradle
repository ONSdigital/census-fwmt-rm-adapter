buildscript {
    ext {
        springBootVersion = '2.1.0.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:latest.release'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'maven'
    id 'maven-publish'
    id 'org.springframework.boot' version '2.1.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'jacoco'
    id 'com.jfrog.artifactory' version '4.8.1'
    id 'org.ajoberstar.grgit' version '3.0.0'
}

mainClassName = 'uk.gov.ons.fwmt.fwmtrmadapter.FwmtRmAdapterApplication'

group = 'uk.gov.ons.fwmt'
version = '3.0.0-SNAPSHOT'
sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenLocal()
    if (!project.hasProperty('omitSDCArtifactory')) {
        maven { url 'http://artifactory-sdc.onsdigital.uk/artifactory/libs-release' }
        maven { url 'http://artifactory-sdc.onsdigital.uk/artifactory/libs-snapshot' }
    }
    mavenCentral()
}

configurations {
    jaxb
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schema = "${projectDir}/src/main/resources/xsd"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask',
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir) {
                //arg(value: '-xsd')
                produces(dir: sourcesDir, includes: '**/*.java')
                schema(dir: schema, includes: '**/*.xsd')
            }

            javac(destdir: classesDir, source: 1.8, target: 1.8, debug: true,
                    debugLevel: 'lines,vars,source',
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: "**/*.java")
                include(name: "*.java")
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: "**/*.java")
                }
            }
        }
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

bootJar {
    baseName = 'fwmt-census-rm-adapter'
    group = 'uk.gov.ons.fwmt'
    version = version
    manifest {
        attributes(['Git-Commit': grgit.head().getAbbreviatedId(8),
                    'Git-Branch': grgit.branch.getCurrent().getName()], 'common')
    }
}

jacoco {
    toolVersion = "0.8.2"
}

test {
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
test.finalizedBy(project.tasks.jacocoTestReport)

check.dependsOn jacocoTestReport

task integrationTest(type: Test) {
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}
check.dependsOn integrationTest
check.dependsOn jacocoTestReport
integrationTest.mustRunAfter test

/*
processResources {
    filter ReplaceTokens, tokens: [
            'project.artifactId': 'name',
            'project.version'   : 'version',
            'scm.url'           : 'url',
            'buildNumber'       : 'buildnumber',
            'scmBranch'         : 'branch',
            'timestamp'         : 'timestamp',
    ]
}
*/

artifactory {
    contextUrl = System.getenv('ARTIFACTORY_URL')
    publish {
        repository {
            repoKey = 'libs-snapshot-local'
            username = System.getenv('ARTIFACTORY_USERNAME')
            password = System.getenv('ARTIFACTORY_PASSWORD')
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'fwmt-census-rm-adapter'
            version = version

            from components.java
        }
    }
}

artifactoryPublish {
    publications 'maven'
}

dependencies {
    jaxb group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    jaxb group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0.1'
    jaxb group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.1'
    jaxb group: 'com.sun.xml.bind', name: 'jaxb-xjc', version: '2.3.1'
    jaxb group: 'javax.activation', name: 'activation', version: '1.1.1'

    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'

    compile files((String) genJaxb.classesDir).builtBy(genJaxb)

    compile group: 'uk.gov.ons.fwmt', name: 'fwmt-gateway-common', version: '3.0.0-RELEASE'

    compile group: 'org.springframework.boot', name: 'spring-boot-starter', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-amqp', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    compile group: 'org.springframework', name: 'spring-aop', version: '5.1.2.RELEASE'
    compile group: 'org.springframework.retry', name: 'spring-retry', version: '1.2.2.RELEASE'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.7'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.7'
    compile group: 'org.json', name: 'json', version: '20180813'

    compile group: 'org.projectlombok', name: 'lombok', version: '1.18.4'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.4'

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion

    integrationTestCompile group: 'org.assertj', name: 'assertj-core', version: '3.11.1'

    integrationTestRuntime group: 'uk.gov.ons.fwmt', name: 'fwmt-service-mocks', version: '3.0.0-SNAPSHOT'
}
